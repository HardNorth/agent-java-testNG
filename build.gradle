/*
 * Copyright 2020 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java-library'

apply from: "${project.scripts_url}/${project.scripts_branch}/build-quality.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/release-commons.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    api 'com.epam.reportportal:client-java:5.0.2'
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api 'com.epam.reportportal:commons-model:5.0.0'
    api 'org.testng:testng:7.1.0'

    testImplementation('com.github.reportportal:agent-java-test-utils:ddcf50ee20')

    testImplementation("org.junit.platform:junit-platform-runner:${junit5_runner_version}") {
        exclude module: 'junit'
    }
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit5_version}"

    testImplementation "org.aspectj:aspectjweaver:1.9.2"

    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockito_version}"
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'com.google.inject:guice:4.2.3'
    testImplementation 'com.epam.reportportal:logger-java-logback:5.0.3'

    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'com.ibm.icu:icu4j:67.1'
}

test {
    useJUnitPlatform()
    outputs.upToDateWhen { false }
    exclude("com/epam/reportportal/testng/integration/feature/**")
    maxParallelForks(5) // it's forks - separate JVMs, should not interfere each other
    doFirst {
        def weaver = configurations.testRuntimeClasspath.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }
    environment "AGENT_NO_ANALYTICS", "1"
}

wrapper {
    gradleVersion = '5.4.1'
}

processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}
